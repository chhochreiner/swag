import metamodel;

extension metamodel::Extensions reexport;

String packagePath(Model this):	
	package.replaceAll("\\.","/");
	
String packagePath(Type this):	
	model(this).package.replaceAll("\\.","/");
	
tableName( Entity this ):
	if isNotEmpty(tableName) then
		tableName
	else 
		name.toFirstLower();

getter( Field this):
	"get" + name.toFirstUpper();

setter( Field this ):
	"set" + name.toFirstUpper();

// TYPE NAMES

typeName( Field this ):
	"'ERROR: YOU SHOULD NEVER EVER SEE THIS, AND IM TOO LAZY TO WRITE A HELPFUL MESSAGE NOW'";

typeName( Attribute this ):
	if ( type.name == "Text" ) then
		"String"
	else 
		type.name;

typeName( Association this ):
	switch ( multiplicity ) {
		case Multiplicity::OneToOne:   type.name
		case Multiplicity::OneToMany:  "List<"+type.name+">"
		case Multiplicity::ManyToOne:  type.name
		case Multiplicity::ManyToMany: "Set<"+type.name+">"
		default:                       "'ERROR: ILLEGAL enum constant : " + multiplicity + "'"
	};

concreteType( Association this ):
	switch ( multiplicity ) {
		case Multiplicity::OneToMany:  "ArrayList<"+type.name+">"
		case Multiplicity::ManyToMany: "HashSet<"+type.name+">"
		default:                       "'ERROR: ILLEGAL enum constant : " + multiplicity + "'"
	};

// DTO TYPE NAMES

dtoTypeName( Type this ):
	this.name;
dtoTypeName( Entity this ):
	this.name + "DTO";

dtoTypeName( Field this ):
	"'ERROR: YOU SHOULD NEVER EVER SEE THIS, AND IM TOO LAZY TO WRITE A HELPFUL MESSAGE NOW'";

dtoTypeName( Attribute this ):
	if ( type.name == "Text" ) then
		"String"
	else
		type.name;

dtoTypeName( Association this ):
	switch ( multiplicity ) {
		case Multiplicity::OneToOne:   type.dtoTypeName()
		case Multiplicity::OneToMany:  "List<"+ type.dtoTypeName() +">"
		case Multiplicity::ManyToOne:  type.dtoTypeName()
		case Multiplicity::ManyToMany: "Set<"+ type.dtoTypeName() +">"
		default:                       "'ERROR: ILLEGAL enum constant : " + multiplicity + "'"
	};

dtoConcreteType( Association this ):
	switch ( multiplicity ) {
		case Multiplicity::OneToMany:  "ArrayList<"+ type.dtoTypeName() +">"
		case Multiplicity::ManyToMany: "HashSet<"+ type.dtoTypeName() +">"
		default:                       "'ERROR: ILLEGAL enum constant : " + multiplicity + "'"
	};	
	
// MULTIPLICTY STUFF

String mappedBy( Association this ):
	if ( isNotEmpty( mappedBy )  ) then
		mappedBy
	else
		switch ( multiplicity ) {
			case Multiplicity::OneToOne:   entity().name.toFirstLower()
			case Multiplicity::OneToMany:  entity().name.toFirstLower()
			case Multiplicity::ManyToOne:  entity().name.toFirstLower()
			case Multiplicity::ManyToMany: entity().name.toFirstLower() + "s"
			default:                       "'ERROR: ILLEGAL enum constant : " + multiplicity + "'"
		}
	;
	